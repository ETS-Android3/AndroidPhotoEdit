plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

def mavenPropertiesFile = rootProject.file("maven.properties")
def mavenProperties = new Properties()
mavenProperties.load(new FileInputStream(mavenPropertiesFile))

def uploadArchivesVersion = {
    if (mavenProperties['release'] == "false") {
        mavenProperties['uploadArchivesVersion'] + "-SNAPSHOT"
    } else {
        mavenProperties['uploadArchivesVersion']
    }
}

android {
    compileSdk 31
    ndkVersion '21.4.7075529'
    defaultConfig {
        minSdk 21
        targetSdk 31
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }


    externalNativeBuild {
        ndkBuild {
            path 'jni/Android.mk'
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        beta {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.getSrcDirs()
    archiveClassifier = "sources"
}

afterEvaluate {
    publishing {
        repositories { RepositoryHandler handler->
            handler.mavenLocal()  // 发布到默认的 本地maven 仓库 ，路径： USER_HOME/.m2/repository/
            maven {//发布到
                allowInsecureProtocol true
                url mavenProperties['releaseUrl']
                credentials {
                    username mavenProperties['userName']
                    password mavenProperties['password']
                }
            }
        }
        publications { PublicationContainer publicationContainer ->
            // 发布正式包
            release(MavenPublication) {
                from components.release // 注释1:使用 Android Gradle 插件生成的组件，作为发布的内容
                artifact sourceJar // 上传源码
                groupId = "com.hprt.wgl"
                artifactId = "photoedit"
                version = uploadArchivesVersion()
            }
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'

    //MVVM组件
    implementation 'com.kunminx.arch:unpeek-livedata:7.2.0-beta1'
    implementation 'com.kunminx.arch:strict-databinding:4.0.0-beta1'
    implementation "com.kunminx.arch:binding-recyclerview:3.9.0-beta1"
    //图像处理
    implementation 'jp.co.cyberagent.android:gpuimage:2.1.0'
    //kotlin图片加载框架
    implementation 'io.coil-kt:coil:1.4.0'
    //沉浸式
    implementation 'com.geyifeng.immersionbar:immersionbar:3.2.1'
    implementation 'com.geyifeng.immersionbar:immersionbar-ktx:3.2.1'
    //拉动条
    implementation 'com.github.yunianshu:IndicatorSeekBarNew:1.0.9'
    //裁剪
    implementation 'com.github.yunianshu:Android-Image-Cropper:4.2.3'
    //工具类
    implementation 'com.blankj:utilcode:1.30.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
//    implementation 'com.github.yunianshu:StickerViewFork:1.0.2'
    implementation project(':sticker')
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    //gson
    implementation 'com.google.code.gson:gson:2.9.0'

    //颜色选择器
    implementation "com.github.skydoves:colorpickerview:2.2.4"

    //glide
    implementation 'com.github.bumptech.glide:glide:4.13.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.0'

    // okdownload核心库
    implementation 'com.liulishuo.okdownload:okdownload:1.0.7'
    //存储断点信息的数据库
    implementation 'com.liulishuo.okdownload:sqlite:1.0.7'
    //提供okhttp连接，如果使用的话，需要引入okhttp网络请求库
    implementation 'com.liulishuo.okdownload:okhttp:1.0.7'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'

    implementation 'com.github.li-xiaojun:XPopup:2.7.6'

    implementation 'com.hprt.sdk:crop:1.0.0@aar'

}